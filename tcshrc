#! /bin/tcsh -x
###############################################################################
# This file is read by tcsh every time you start a shell.
###############################################################################
# For CCS machines:
# This file loads the central .tcshrc that is stored in /ccs/etc/dotfiles.
# This is done because the Systems Group will occasionally update that 
# file to reflect changes in the network, and you may be better off if
# those changes are made for you.
###############################################################################
# Matt Bisson

# If we've not seen the login script, we need to load it.  Also, if we have
# seen the thing, but were configured for a different shell, we need to
# reload it.
if (${?USING_TCSH}) then
    if (${USING_TCSH}) then
        if (! {$?version}) then
           # Was set for tcsh, now using csh
           source ${HOME}/.login
           exit
        endif
    else
        if ({$?version}) then
           # Was set for csh, now using tcsh
           source ${HOME}/.login
           exit
        endif
    endif
else
    # We've never seen the LOGIN script
    source ${HOME}/.login
    exit
endif

# This is the only way I know of to determine if I am on a CCS computer
# Place CCS only commands here...
if (-f "/ccs/etc/dotfiles/.tcshrc") then
    source "/ccs/etc/dotfiles/.tcshrc"
endif

# Put csh aliases all in one file, in order to collect them in one
# place.  If you create more aliases, just add them to that file. 
if (-f "${HOME}/.alias.csh") then
    source "${HOME}/.alias.csh"
endif

###############################################################################
# Miscellaneous settings
###############################################################################

if (${USING_TCSH}) then
    set   autocomplete                  # TAB does this (not just ^D)
    unset autocorrect                   # Don't fix spelling mistakes?
    set   autolist    = 'ambiguous'
    unset autologout
    unset correct
    unset complete                      # Case SENSITIVE autocomplete
    set   listflags   = 'a'
    set   listjobs    = 'long'
    set   listmaxrows = 50
    set   matchbeep   = 'nomatch'
    set   savehistory = 'merge'

    # Use emacs keybindings
    bindkey -e
    bindkey "^[[1;5D" backward-word     # Ctrl + [left]
    bindkey "^[[1;5C" forward-word      # Ctrl + [right]
    bindkey "^[[3~"   delete-char       # [delete]
endif

unset fignore                           # Don't ignore anything in completion
set   history = 128
unset ignoreeof                         # I want to log out with ^D

# Using stuff set in .login to get the prompt
if (${?PROMPT}) then
    if ((${USING_TCSH}) && (1 == ${?TERM})) then
        switch (${TERM})
            case "cygwin"
            case "dtterm"
            case "linux"
            case "rxvt"
            case "xterm"
            case "xterm-color"
            case "xterm-xfree86"
                set prompt="%{^[[01;39m%}${PROMPT}\%%{^[[00m%} "
                breaksw
            default
                set prompt="${PROMPT}\% "
                breaksw
        endsw
    else
        set prompt="${PROMPT}% "
    endif
endif
if (${?PROMPT2}) then
    set prompt2="${PROMPT2}"
endif
if (${?PROMPT3}) then
    set prompt3="${PROMPT3}"
endif

##############################################################################
# Set limits for the environment
##############################################################################
if (`uname -s` != "CYGWIN_NT-5.1") then # Cygwin doesn't do "limit"
    limit coredumpsize    unlimited         # Core file size (blocks)
    #limit datasize        unlimited        # Data seg size (kbytes)
    #limit filesize        unlimited        # File size (blocks)
    #limit memorylocked    unlimited        # Max locked memory (kbytes)
    #limit memoryuse       unlimited        # Max memory size (kbytes)
    #limit descriptors     1024             # Open file descriptors
    #limit openfiles       1024             # Open files
    #limit stacksize       8192 kbytes      # Stack size (kbytes)
    #limit cputime         unlimited        # CPU time (seconds)
    #limit maxproc         2048             # Max user processes
endif
